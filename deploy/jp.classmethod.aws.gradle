apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

publishing {
	publications {
		mavenStuff(MavenPublication) {
			from components.java

			artifact sourcesJar {
				classifier "sources"
			}

			artifact javadocJar {
				classifier "javadoc"
			}
		}
	}
}

if (project.hasProperty('awsAccessKey') && project.hasProperty('awsSecretKey')) {
	uploadArchives {
		repositories {
			mavenDeployer {
				def awsCredentials = [
					userName: awsAccessKey,
					passphrase: awsSecretKey
				]
	
				configuration = configurations.deployerJars
				repository(url: "s3://public-maven.classmethod.info/release") {
					authentication(awsCredentials)
				}
				snapshotRepository(url: "s3://public-maven.classmethod.info/snapshot") {
					authentication(awsCredentials)
				}
			}
		}
	}
	
	[install.repositories.mavenInstaller, uploadArchives.repositories.mavenDeployer]*.pom*.whenConfigured { pom ->
		pom.project {
			inceptionYear '2013'
			packaging 'jar'
			licenses {
				license {
					name 'The Apache Software License, Version 2.0'
					url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					distribution 'repo'
				}
			}
		}
	}
}


if (project.hasProperty('bintrayUser') && project.hasProperty('bintrayApiKey')) {
	bintray {
		user = bintrayUser
		key = bintrayApiKey
	
		publications = ['mavenStuff']
		pkg {
			repo = 'maven'
			name = "gradle-aws-plugin"
			desc = 'Gradle plugin to manage AWS resources'
			userOrg = 'classmethod'
			websiteUrl = 'https://github.com/classmethod-aws/gradle-aws-plugin'
			issueTrackerUrl = 'https://github.com/classmethod-aws/gradle-aws-plugin/issues'
			vcsUrl = 'https://github.com/classmethod-aws/gradle-aws-plugin.git'
			licenses = ['Apache-2.0']
			labels = ['AWS', 'Gradle']
		}
	}
}

pluginBundle {
	website = 'https://github.com/classmethod-aws/gradle-aws-plugin'
	vcsUrl = 'https://github.com/classmethod-aws/gradle-aws-plugin'
	description = 'Gradle plugin to manage Amazon Web Services'
	tags = ['AWS']

	plugins {
		awsPlugin {
			id = 'jp.classmethod.aws'
			displayName = 'Gradle AWS plugin'
		}
		awsBeanstalkPlugin {
			id = 'jp.classmethod.aws.beanstalk'
			displayName = 'Gradle AWS Elastic Beanstalk plugin'
		}
		awsCloudFormationPlugin {
			id = 'jp.classmethod.aws.cloudformation'
			displayName = 'Gradle AWS CloudFormation plugin'
		}
		awsCloudWatchPlugin {
			id = 'jp.classmethod.aws.cloudwatch'
			displayName = 'Gradle Amazon CloudWatch plugin'
		}
		awsECRPlugin {
			id = 'jp.classmethod.aws.ecr'
			displayName = 'Gradle Amazon ECR plugin'
		}
		awsEC2Plugin {
			id = 'jp.classmethod.aws.ec2'
			displayName = 'Gradle Amazon EC2 plugin'
		}
		awsELBPlugin {
			id = 'jp.classmethod.aws.elb'
			displayName = 'Gradle Amazon Elastic Load Balancing plugin'
		}
		awsIAMPlugin {
			id = 'jp.classmethod.aws.iam'
			displayName = 'Gradle AWS Identity and Access Management plugin'
		}
		awsLambdaPlugin {
			id = 'jp.classmethod.aws.lambda'
			displayName = 'Gradle AWS Lambda plugin'
		}
		awsRDSPlugin {
			id = 'jp.classmethod.aws.rds'
			displayName = 'Gradle Amazon RDS plugin'
		}
		awsRoute53Plugin {
			id = 'jp.classmethod.aws.route53'
			displayName = 'Gradle Amazon Route53 plugin'
		}
		awsS3Plugin {
			id = 'jp.classmethod.aws.s3'
			displayName = 'Gradle Amazon S3 plugin'
		}
		awsSSMPlugin {
			id = 'jp.classmethod.aws.ssm'
			displayName = 'Gradle Amazon SSM plugin'
		}
	}
	mavenCoordinates {
		groupId = project.group
		artifactId = project.artifactId
		version = project.version
	}
}
